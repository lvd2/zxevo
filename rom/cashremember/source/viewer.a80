
;LAST UPDATE: 24.07.2017 savelij

		include ../macros.a80

;VIEWER  EQU 25000
VIEWER  EQU	0x8800
FONT58  EQU	0x8000
;TEXTADR EQU 0x8800
TEXTADR EQU	0x8F00

;        ORG	FONT58
;        INCBIN	"FONT5"

;        ORG	TEXTADR
;        INCBIN	"cash2_0h"
;        DB	"THIS IS END OF TEXT",13,10,3

eCODE   EQU	$-VIEWER
eCODEs  EQU	VIEWER
eCODEe  EQU	$

eTEXT   EQU	$-TEXTADR
eTEXTs  EQU	TEXTADR
eTEXTe  EQU	$

;-------

LDA        MACRO P0,P1
        LD	A,P1
        LD	P0,A
        ENDM	

madd        MACRO P0,P1
        LD	A,P0
        ADD	A,P1
        LD	P0,A
        ENDM	

;---

cls        MACRO P0,P1
        LD	HL,P0
        LD	DE,P0+1
        LD	BC,P1-1
        LD	(HL),0
        LDIR	
        ENDM	

key        MACRO P0
        LD	A,P0
        IN	A,(254)
        ENDM	

rjr        MACRO P0
        RRA	
        JR	NC,P0
        ENDM	

rjp        MACRO P0
        RRA	
        JP	NC,P0
        ENDM	

;-------

inikub        MACRO
        LD	A,(HL)
        INC	HL
        CP	C
        RET	Z
        EXX	
        LD	D,HIGH (FONT58)
        LD	E,A
        ENDM	

rot1        MACRO P0,P1,P2
        LD	B,P2
x1 := 8
        REPT	8
        LD	A,(DE)
        DUPL	P1,P0*8+7
        AND	B
        OR	(HL)
        LD	(HL),A
x1 := x1-1
        IF	x1
        INC	H
        INC	D
        ENDIF	
        ENDM	
        madd H,-7
        ENDM	

rot2        MACRO P0,P1,P2
        LD	B,P2
x1 := 4
        REPT	4
        LD	A,(DE)
        DUPL	P1,P0*8+7
        LD	C,A
        AND	B
        OR	(HL)
        LD	(HL),A
        INC	L
        LD	A,C
        AND	255-P2
        LD	(HL),A
        INC	H
        INC	D
        LD	A,(DE)
        DUPL	P1,P0*8+7
        LD	C,A
        AND	255-P2
        LD	(HL),A
        DEC	L
        LD	A,C
        AND	B
        OR	(HL)
        LD	(HL),A
x1 := x1-1
        IF	x1
        INC	H
        INC	D
        ENDIF	
        ENDM
        madd H,-7
        ENDM	

;-------



;-------
;VIEWER;
;-------

        ORG	VIEWER

        LD	HL,TEXTADR
        LD	(A1_TEXT+1),HL
        LD	A,(HL)
        INC	HL
        CP	3
        JR	NZ,$-4
        LD	B,26
        CALL	BACKLIN
        LD	(A2_TEXT+1),HL
        LD	HL,0x5800
        LD	DE,0x5801
        LD	BC,0x2FF
        LD	(HL),7
        LDIR	

L1TXKEY CALL	TEXTOUT

TXT_KEY key 0xDF
        rjr TXT_RIG
        rjr TXT_LEF
        key 0xFD
        rjr TXT_DWN
        key 0xFB
        rjr TXT_UP
        key 0x7F
        RRA	
        JR	C,TXT_KEY

        LD	HL,10072
        EXX	
        RET	

TXT_RIG LD	HL,000
        LD	B,2
        LD	A,(HL)
        CP	3
        JR	Z,TXT_KEY
        JR	TXT_LEF+5

TXT_LEF LD	HL,(A1_TEXT+1)
        LD	B,24
        CALL	BACKLIN
        EX	DE,HL
        LD	HL,(A1_TEXT+1)
        OR	A
        SBC	HL,DE
        JR	Z,TXT_KEY
        EX	DE,HL
        LD	(A1_TEXT+1),HL
        JR	L1TXKEY

TXT_DWN LD	A,0
        OR	A
        JR	Z,TXT_KEY
        LD	HL,(TXT_RIG+1)
        LD	A,(HL)
        CP	3
        JR	Z,TXT_KEY
        PUSH	HL
;        LOCAL	
        LD	HL,(A1_TEXT+1)       ;NEXT LINE
.J1      LD	A,(HL)
        INC	HL
        CP	13
        JR	NZ,.J1
        INC	HL
        LD	(A1_TEXT+1),HL
;        ENDL	
        CALL	SCR_UP
        POP	HL
        EXX	
        LD	HL,0x50E0
        EXX	
        CALL	L1_TEXT
        JR	TXT_KEY

TXT_UP  LD	HL,(A1_TEXT+1)
        LD	B,2
        CALL	BACKLIN
        EX	DE,HL
        LD	HL,(A1_TEXT+1)
        OR	A
        SBC	HL,DE
        JP	Z,TXT_KEY

        XOR	A                   ;CAN'T SCROLL UP
        LD	(TXT_DWN+1),A
        PUSH	DE
A2_TEXT LD	HL,000
        SBC	HL,DE
        JR	C,L2_TEXT

        LD	HL,(TXT_RIG+1)
        LD	B,2
        CALL	BACKLIN
        LD	(TXT_DWN+1),A        ;CAN SCROLL UP
        LD	(TXT_RIG+1),HL
L2_TEXT CALL	SCR_DWN
        POP	HL

        LD	(A1_TEXT+1),HL
        EXX	
        LD	HL,0x4000
        EXX	
        CALL	STROKA
        JP	TXT_KEY

;---

BACKLIN DEC	HL
        LD	A,H
        CP	HIGH (TEXTADR)
        JR	NC,L1BACKL
        LD	HL,TEXTADR
        RET	
L1BACKL LD	A,(HL)
        CP	13
        JR	NZ,BACKLIN
        DJNZ	BACKLIN
        INC	HL
        INC	HL
        RET	

;---

SCR_DWN
x2 := 0x5000
        REPT	3
x1 := 6
        REPT	7
        LD	HL,x1*0x20+x2
        LD	DE,x1*0x20+x2+0x20
        CALL	SCR_LIN
x1 := x1-1
        ENDM	
        IF	x2-0x4000
        LD	HL,x2-0x720
        LD	DE,x2
        CALL	SCR_LIN
        ENDIF	
x2 := x2-0x800
        ENDM	
        LD	HL,0x4000
        JP	CLS_LIN

SCR_UP
x2 := 0x4000
        REPT	3
x1 := 0
        REPT	7
        LD	HL,x1*0x20+x2+0x20
        LD	DE,x1*0x20+x2
        CALL	SCR_LIN
x1 := x1+1
        ENDM	
        IF	x2-0x5000
        LD	HL,x2+0x800
        LD	DE,x2+0xE0
        CALL	SCR_LIN
        ENDIF	
x2 := x2+0x800
        ENDM	
        LD	HL,0x50E0

CLS_LIN LD	C,8
;        LOCAL	
.J2      LD	B,32
        XOR	A
.J1      LD	(HL),A
        INC	L
        DJNZ	.J1
        madd L,0xE0
        INC	H
        DEC	C
        JR	NZ,.J2
;        ENDL	
        RET	

SCR_LIN EXX	
        LD	B,4
;        LOCAL	
.J1      EXX	
        REPT	31
        LDI	
        ENDM	
        LDA (DE),(HL)
        INC	H
        INC	D
        REPT	31
        LDD	
        ENDM	
        LDA (DE),(HL)
        INC	H
        INC	D
        EXX	
        DEC	B
        JP	NZ,.J1
;        ENDL	
        RET	

;---

TEXTOUT cls 16384,6144
A1_TEXT LD	HL,000
        EXX	
        LD	HL,0x4000
        EXX	
L1_TEXT XOR	A                   ;CAN'T SCROLL DOWN
        LD	(TXT_DWN+1),A
;        LOCAL	
.J1      LD	A,(HL)
        CP	3
        JR	Z,.L1
        CALL	STROKA
        INC	HL
        EXX	
        LD	A,L
        AND	0xE0
        ADD	A,0x20
        LD	L,A
        EXX	
        JR	NZ,.J1
        EXX	
        madd H,8
        CP	0x58
        EXX	
        JR	NZ,.J1
        LD	(TXT_DWN+1),A        ;CAN SCROLL DOWN
.L1      LD	(TXT_RIG+1),HL
;        ENDL	
        RET	

STROKA  LD	BC,0x70D
KUB0    inikub
        REPT	7
        LDA (HL),(DE)
        INC	H
        INC	D
        ENDM	
        LDA (HL),(DE)
        madd H,-7
        EXX	
KUB1    inikub
        rot2 0,3,7
        INC	L
        EXX	
KUB2    inikub
        rot1 1,2,0x3E
        EXX	
KUB3    inikub
        rot2 0,1,1
        INC	L
        EXX	
KUB4    inikub
        rot2 0,4,0x0F
        INC	L
        EXX	
KUB5    inikub
        rot1 1,1,0x7C
        EXX	
KUB6    inikub
        rot2 0,2,3
        INC	L
        EXX	
KUB7    inikub
        rot1 1,3,0x1F
        INC	L
        EXX	
        DEC	B
        JP	NZ,KUB0
        RET	

eVIEWER EQU	$-VIEWER
eVIEWs  EQU	VIEWER
eVIEWe  EQU	$

		DUPL LOW (-$),0
		binclude cash2_0h.txt
        DB	"THIS IS END OF TEXT",13,10,3
