
;LAST UPDATE: 28.07.2017 savelij

;eINSTAL EQU	$-25000

;---------
;BANK 144;
;---------

;		PHASE 0XC000	;ORG	0xC000;,CSH_BNK

        LD	HL,0xFFBF     ;SHUT (AY) UP!!!
        LD	C,0xFD
        LD	A,8
;        LOCAL	
.J1      LD	B,H
        OUT	(C),A
        LD	B,L
        EXA
        XOR	A
        OUT	(C),A
        EXA
        INC	A
        CP	11
        JR	NZ,.J1
;        ENDL	

TYTMAIN CALL	TYTLMAK
        CALL	TYTLOUT

TYTLKEY key 0xF7
        LD	HL,SAVE_T1
        LD	BC,0x1701
        rjr CH_ESC      ;1 - SAVE.
        LD	HL,LOAD_T1
        LD	BC,0xF02
        rjr CH_ESC      ;2 - LOAD.
        LD	HL,EXIT_T1
        LD	BC,0x2800
        rjr CH_ESC      ;3 - ESC.
        rjr CH_SCR      ;4 - SCR.
        rjr CH_ORG      ;5 - ORG.
        key 0xEF
        rjp TYTLOFF     ;0,9,8 - TYTL.
        LD	HL,TYTLOUT+1
        rjp TYTL_UP
        rjp TYTL_DW
        rjr TYT_SC2     ;7 - 2nd Scr.
        rjr CH_MEM      ;6 - POS.
        key 0x7F         ;M - STS.
        BIT	2,A
        LD	HL,STS_T1
        LD	BC,0x2803
        JR	Z,CH_ESC
        key 0xDF
        RRA	
        RRA	
        rjp INFINIT     ;I - INFINITISER
        rjp UTILITE     ;U - UTILIZER
        key 0xFD         ;D - DISK
        BIT	2,A
        JP	Z,TRDOS
        key 0xBF
        BIT	4,A
        JP	Z,HELPER
        JR	TYTLKEY

CH_MEM  LD	HL,XX_POS+1
        LD	A,(HL)
        XOR	0x80
        JR	L1CHMEM
CH_ORG  LD	HL,XX_ORG+1
        LD	A,(HL)
        XOR	32
        JR	L1CHMEM
CH_SCR  LD	HL,XX_BANK+1
        LD	A,(HL)
        XOR	8
L1CHMEM LD	(HL),A
        CALL	ANTYKEY
        JP	TYTMAIN
CH_ESC  LDA (ESC_COM+1),C
        LD	A,B
        CALL	PRINTIN
        CALL	YESORNO
        JP	NZ,TYTMAIN
        LDA (XX_ORG+1),32
        RET	

TYT_SC2 bank 144+8
        CALL	ANTYKEY
        LD	A,144
        OUT	(C),A
        JP	TYTMAIN

TYTLOFF CALL	REM_SCR
        CALL	ANTYKEY
        JP	TYTMAIN

TYTL_UP LD	A,(HL)
        OR	A
        JP	Z,TYTLKEY
        DEC	(HL)
        LD	C,(HL)
        LD	B,8
J1TYTUP PUSH	BC
        LD	A,C
        CALL	VIDEOAT    ;DE = LINE (x)
        EX	DE,HL
        LD	A,C
        INC	A
        CALL	VIDEOAT    ;HL = LINE (x+1)
        CALL	MOVLINE
        POP	BC
        INC	C
        DJNZ	J1TYTUP
        LD	A,C
        CALL	VIDEOAT
        CALL	REM_LIN
        JP	TYTLKEY

TYTL_DW LD	A,(HL)
        ADD	A,8
        CP	24
        JP	Z,TYTLKEY
        INC	(HL)
        LD	C,A
        LD	B,8
J1TYTDW PUSH	BC
        LD	A,C
        CALL	VIDEOAT    ;DE = LINE (x+8)
        EX	DE,HL
        LD	A,C
        DEC	A
        CALL	VIDEOAT    ;HL = LINE (x+8-1)
        CALL	MOVLINE
        POP	BC
        DEC	C
        DJNZ	J1TYTDW
        LD	A,C
        CALL	VIDEOAT
        CALL	REM_LIN
        JP	TYTLKEY

MOVLINE LD	C,16         ;MOVING PNTS
;        LOCAL	
.J3      LD	B,8
.J1      LD	A,(HL)
        LD	(DE),A
        INC	H
        INC	D
        DJNZ	.J1
        INC	L
        INC	E
        LD	B,8
.J2      DEC	H
        DEC	D
        LD	A,(HL)
        LD	(DE),A
        DJNZ	.J2
        INC	L
        INC	E
        DEC	C
        JR	NZ,.J3

        LD	A,H          ;MOVING ATTR
        SUB	0x40
        RRCA	
        RRCA	
        RRCA	
        OR	0x58
        LD	H,A
        DEC	L
        LD	A,D
        SUB	0x40
        RRCA	
        RRCA	
        RRCA	
        OR	0x58
        LD	D,A
        DEC	E

        LD	B,32
.J4      LD	A,(HL)
        LD	(DE),A
        DEC	L
        DEC	E
        DJNZ	.J4

;        ENDL	
        RET	

TYTLOUT LD	A,000        ;PRINT TYTLE AT ANY LINE
        CALL	PRINTAT
        CALL	INK5

        LD	HL,TYTLE_T   ;TYTLE TEXT AND ESCAPE TEXT
;        LOCAL	
        LD	B,2
.J1      PUSH	BC
        CALL	TXTOUT5
        CALL	SYM5CLR
        CALL	PRINTLN
        POP	BC
        DJNZ	.J1
        LD	B,3
.J2      PUSH	BC         ;OPTIONS 1,2,3,...8,9,0
        CALL	TXTOUT5
        CALL	SYM5CLR
        CALL	NEXTSTR
        POP	BC
        DJNZ	.J2
        LDA (SYMINK+1),4
        LD	B,5
.J3      PUSH	BC         ;OPTIONS D,M,I,A,U
        LD	A,(HL)
        INC	HL
        PUSH	HL

        LD	DE,(SYMOUT5+3)       ;PRINT 1st LETTER (INVERSE)
        LD	H,HIGH (FONT58);'FONT58
        LD	L,A
        LD	B,8
.J4      LD	A,(HL)
        RRCA	
        CPL	
        AND	0xFC
        LD	C,A
        LD	A,(DE)
        AND	1
        OR	C
        LD	(DE),A
        INC	H
        INC	D
        DJNZ	.J4

        LDA (SYMOUT5+1),7       ;PRINT 2nd AND OTHER LETTERS
        POP	HL
        CALL	TXTOUT5
        POP	BC
        DJNZ	.J3
        CALL	SYM5CLR
;        ENDL	
        RET	

TYTLMAK LD	A,(XX_BANK+1)
        LD	HL,ESC_T     ;BANK 000
        CALL	NUMT_A

        LD	DE,5
        ADD	HL,DE
XX_ORG0 LD	DE,25000     ;ORG 00000
XX_ORG  LD	A,32
        OR	A
        JR	NZ,$+7
        LD	A,"+"
XX_ORG1 LD	DE,50000     ;ORG+00000
        LD	(HL),A
        INC	HL
        CALL	NUMT_DE

        INC	HL
        INC	HL
        LD	A,(XX_DI)
        LD	C,"D"
        CP	243                  ;DI/EI
        JR	Z,$+3
        INC	C
        LD	(HL),C
        LD	DE,7
        ADD	HL,DE
        LD	A,(XX_IM+1)          ;IM 1/2
        LD	C,"1"
        CP	0x56
        JR	Z,$+3
        INC	C
        LD	(HL),C
        LD	DE,13                ;POS 1/2
        ADD	HL,DE
        LD	A,(XX_POS+1)
        LD	C,"1"
        CP	0x40
        JR	Z,$+3
        INC	C
        LD	(HL),C
        LD	HL,SAVE_T1+40
        LD	(HL),C
        LD	HL,LOAD_T1+40
        LD	(HL),C
        RET	

;---

ANTYKEY key 0x81         ;ALL KEYS EXCLUDING CS-Z-X-...-M-SS-SP
        CPL	
        AND	31
        JR	NZ,ANTYKEY
        key 0xFE         ;KEYS Z-X-C-V
        CPL	
        AND	30
        JR	NZ,ANTYKEY
        key 0x7F
        CPL	
        AND	29          ;KEYS SP-M-N-B
        JR	NZ,ANTYKEY
        RET	

ANYKEY  XOR	A
        IN	A,(254)
        CPL	
        AND	31
        JR	Z,ANYKEY
        RET	

YESORNO key 0xDF ;Y-YES, Z
        BIT	4,A
        RET	Z
        key 0x7F
        BIT	3,A
        JR	NZ,YESORNO
        OR	A    ;N-NO, NZ
        RET	

;---

REGSAVE CALL	REGADR     ;SAVE REGISTERS + VARIABLES
        LDIR	
        LD	HL,XX_BANK+1
        LDI	
        LD	HL,XX_DI
        LDI	
        LD	HL,XX_IM+1
        LDI	
        JP	AFTECOP

REGLOAD CALL	REGADR     ;LOAD REGISTERS + VARIABLES
        EX	DE,HL
        LDIR	
        LD	DE,XX_BANK+1
        LDI	
        LD	DE,XX_DI
        LDI	
        LD	DE,XX_IM+1
        LDI	
        JP	AFTECOP

REGADR  LD	HL,102-22
        LD	BC,22
        LD	A,(XX_POS+1)
        LD	DE,REGS1
        CP	0x40
        RET	Z
        LD	DE,REGS2
        RET	

;-------------
;DISASSEMBLER;
;-------------

		include disassem.a80	;INCLUDE	"DISASSEM.H",0x41

;-----------
;INFINITISER
;-----------

		include infinitr.a80	;INCLUDE	"INFINITR.H",0x42

;---------------
;PRINTING PROGS;
;---------------

PRINTIN LD	(SYMINK+1),A
        PUSH	HL
        CALL	PRINTIn
        POP	HL
        CALL	TXTOUT5
        JP	INK5

PRINTIn LD	A,(TYTLOUT+1);PREPARE FOR PRINT INSIDE TYTLE
        ADD	A,2

PRINTAT CALL	VIDEOAT    ;PREPARE FOR PRINT AT LINE A
PRINTHL LD	(SYMOUT5+3),HL
        XOR	A
        LD	(SYMOUT5+1),A
        RET	

VIDEOAT LD	C,A          ;HL = VIDEO ADDR OF LINE A
        AND	7
        RRCA	
        RRCA	
        RRCA	
        LD	L,A
        LD	A,C
        AND	0x18
        OR	0x40
        LD	H,A
        RET	

PRINTLN CALL	NEXTSTR            ;PRINT 51 GREEN "*"
        LD	BC,0x3300+'*'
        PUSH	HL
        CALL	PRINT_C
        CALL	SYM5CLR
        POP	HL
NEXTSTR XOR	A
        LD	(SYMOUT5+1),A
;        LOCAL	
        PUSH	HL
        LD	HL,(SYMOUT5+3)
        LD	A,L
        AND	0xE0
        ADD	A,0x20
        LD	L,A
        JR	NC,.L1
        madd H,8
.L1      LD	(SYMOUT5+3),HL
        POP	HL
;        ENDL	
        RET	

PRINT_C LD	HL,SYMINK+1
        DEC	(HL)
        PUSH	HL
;        LOCAL	
.J1      PUSH	BC
        LD	A,C
        CALL	SYMOUT5
        POP	BC
        DJNZ	.J1
        POP	HL
        INC	(HL)
;        ENDL	
        RET	

PRINTbd LD	(.A1+1),A             ;SOME TEXT
        EX	DE,HL
        CALL	PRINTHL
        EX	DE,HL
        CALL	TXTOUT5
;        LOCAL	
        LD	A,"["                ;[
        CALL	SYMOUT5
        LDA L,(SYMOUT5+1)       ;STORE POSITION FOR "..."
        LDA H,(SYMOUT5+3)
        PUSH	HL                 ;" "s
.A1      LD	B,0
.J1      PUSH	BC
        LD	A," "
        CALL	SYMOUT5
        POP	BC
        DJNZ	.J1
        LD	A,"]"                ;]
        CALL	SYMOUT5
        CALL	SYM5CLR
        POP	HL
        LDA (SYMOUT5+1),L       ;RESTORE POSITION FOR 1st "."
        LDA (SYMOUT5+3),H
;        ENDL	
        RET	

TRPRINT LD	(SYMINK+1),A
        LD	(INK3D13+1),A
        PUSH	HL
        CALL	PRINTIn
        POP	HL
        LD	DE,TRD_TXT
        PUSH	DE
        LD	BC,52
        LDIR	
        POP	HL
        CALL	TXTOUT5
INK5    LDA (SYMINK+1),5
        RET	

;--------------
;PRINT NUMBERS;
;--------------

NUMT_DE EX	DE,HL
;        LOCAL	
        LD	BC,10000
        CALL	.S1
        LD	BC,1000
        CALL	.S1
        LD	BC,100
        CALL	.S1
        LD	A,L
        EX	DE,HL
        JR	L1NUMTA
.S1      LD	A,'0'-1
        OR	A
.J1      INC	A
        SBC	HL,BC
        JR	NC,.J1
        ADD	HL,BC
        LD	(DE),A
        INC	DE
;        ENDL	
        RET	

NUMT_A  LD	B,100
        CALL	S1NUMTA
L1NUMTA LD	B,10
        CALL	S1NUMTA
        LD	B,1
S1NUMTA LD	C,'0'-1
;        LOCAL	
.J1      INC	C
        SUB	B
        JR	NC,.J1
        ADD	A,B
        LD	(HL),C
        INC	HL
;        ENDL	
        RET	

NUM5_HL LD	D,"0"
;        LOCAL	
        LD	BC,10000
        CALL	.S1
        LD	BC,1000
        CALL	.S1
        LD	BC,100
        CALL	.S1
        JR	L1NUM5A

.S1      LD	A,'0'-1
        OR	A
.J1      INC	A
        SBC	HL,BC
        JR	NC,.J1
        ADD	HL,BC
        CP	D            ;IF D=A="0" TH DO NOTHING
        RET	Z
        PUSH	HL
        CALL	SYMOUT5    ;SYMOUT5 MAKES D=0x58,0x59,0x5A
        POP	HL
;        ENDL	
        RET	

NUM5_L  LD	D,"0"
        LD	B,100
        CALL	S1NUM5A
L1NUM5A LD	B,10
        CALL	S1NUM5A
        LD	B,1
        LD	D,B
S1NUM5A LD	C,'0'-1
        LD	A,L
        INC	C
        SUB	B
        JR	NC,$-2
        ADD	A,B
        LD	L,A
        LD	A,C
        CP	D
        RET	Z
        PUSH	HL
        CALL	SYMOUT5
        POP	HL
        RET	

;-----------
;ATTR. CLEAR
;-----------

SYM5CLR LD	A,(SYMOUT5+1)
        OR	A
        RET	Z
        LD	B,A
        LD	A,255
;        LOCAL	
.J1      SRL	A
        DJNZ	.J1
        CPL	
        LD	C,A
        PUSH	HL
        LD	HL,(SYMOUT5+3)
        LD	B,8
.J2      LD	A,(HL)
        AND	C
        LD	(HL),A
        INC	H
        DJNZ	.J2
        POP	HL
;        ENDL	
        RET	

;---------------
;PRINT TEXT 5x8;
;---------------

TXTOUT5 LD	A,(HL)
        INC	HL
;        LOCAL	
        CP	32
        JR	C,.L1         ;32 - TEXT
        PUSH	HL
        CALL	SYMOUT5
        POP	HL
        JR	TXTOUT5

.L1      OR	A            ;0 - END OF TEXT
        RET	Z
        PUSH	AF
        BIT	3,A
        JR	Z,.L2
        RLCA	
        RLCA	
        RLCA	
        AND	0x38
        JR	.L2+2

.L2      AND	7           ;INK OF SYMBOL
        LD	(SYMINK+1),A

        LD	A,(HL)       ;E=Y*32
        AND	7
        RRCA	
        RRCA	
        RRCA	
        LD	E,A
        LD	A,(HL)       ;D=0x40,0x48,0x50
        INC	HL
        AND	0x18
        OR	0x40
        LD	D,A
        LD	(SYMOUT5+3),DE
        XOR	A
        LD	(SYMOUT5+1),A

        POP	AF
        AND	16
        JR	Z,TXTOUT5

        PUSH	HL         ;HL=TEXT
        LD	C,255        ;C=TEXT LENTH
.J1      LD	A,(HL)
        INC	HL
        INC	C
        CP	32
        JR	NC,.J1

        LD	A,C          ;C=128-(C*5/2)
        ADD	A,A
        ADD	A,A
        ADD	A,C
        SRL	A
        LD	C,A
        LD	A,128
        SUB	C
        LD	C,A

        AND	7
        LD	(SYMOUT5+1),A
        LD	A,C
        RRA	
        RRA	
        RRA	
        AND	31
        LD	C,A
        LD	A,E
        AND	0xE0
        OR	C
        LD	(SYMOUT5+3),A

        POP	HL
        JR	TXTOUT5
;        ENDL	

;-------------;
;PRINT SYM 5x8;
;-------------;

SYMOUT5 LD	C,0
        LD	DE,0
;        LOCAL	
        LD	L,A
        LD	H,HIGH (FONT58);'FONT58

        LD	A,C
        OR	A
        JR	Z,.S0
        SUB	4
        JR	C,.S1
        INC	A
        LD	C,A

        LD	B,C                  ;SYMOUT ROL=4,5,6,7
        LD	A,0xE0
        SRA	A
        DJNZ	$-2
.L1S2    LD	(.A1S2+1),A
        LD	B,C
        LD	A,0xFF
        SRL	A
        DJNZ	$-2
        LD	(.A2S2+1),A

        LD	A,8
.J1S2    EXA
        LD	A,(HL)
        RRCA	
        RRCA	
        RRCA	
        PUSH	AF
        LD	B,C
        SRL	A
        DJNZ	$-2

        LD	B,A
        LD	A,(DE)
.A1S2    AND	0
        OR	B
        LD	(DE),A
        INC	E

        POP	AF
        LD	B,C
        RRCA	
        DJNZ	$-1
        AND	0xF0

        LD	B,A
        LD	A,(DE)
.A2S2    AND	0
        OR	B
        LD	(DE),A
        DEC	E
        INC	D
        INC	H

        EXA
        DEC	A
        JR	NZ,.J1S2
        JR	SYMINK.E0

.S0      LD	B,8                  ;SYMOUT ROL=0
        LD	A,(DE)
        AND	7
        OR	(HL)
        LD	(DE),A
        INC	D
        INC	H
        DJNZ	.S0+2
        JR	SYMINK.E0

.S1      LD	B,C                  ;SYMOUT ROL=1,2,3
        LD	A,7
        RRCA	
        DJNZ	$-1
        LD	(.A1S1+1),A

        LD	A,8
.J1S1    EXA
        LD	A,(HL)
        LD	B,C
        RRCA	
        DJNZ	$-1
        LD	B,A
        LD	A,(DE)
.A1S1    AND	0
        OR	B
        LD	(DE),A
        INC	D
        INC	H
        EXA
        DEC	A
        JR	NZ,.J1S1

SYMINK
.E0      LD	C,5
        LD	A,D                  ;A=0x48,0x50,0x58
        SUB	0x48
        RRCA	
        RRCA	
        RRCA	
        OR	0x58
        LD	D,A
        LD	A,C
        LD	(DE),A

.E1      LD	HL,SYMOUT5+1         ;x=x+5
        madd (HL),5
        SUB	8
        RET	C
        LD	(HL),A
        INC	HL
        INC	HL
        INC	(HL)
        OR	A
        RET	Z
        INC	E
        LD	A,C
        LD	(DE),A

;        ENDL	
        RET	

;-------

TYTLE_T DB	127," 2013 Flyman512's Cash Remember v2.01 for Pent"
        DB	"512K",0
        DB	"Escape: Bank "
ESC_T   DB	"000; Org 00000; ?I; IM ?. Position: ?.",0
        DB	"1 - Save.   4 - Select Screen.    7 - View 2nd Scr."
        DB	0
        DB	"2 - Load.   5 - View Another Org. 8/9 - Move Tytle."
        DB	0
        DB	"3 - Escape. 6 - Select Position.  0 - Remove Tytle."
        DB	0
        DB	"Disk, ",0,"Monitor,  ",0,"Infinitizer,  ",0
        DB	"Utilites,  ",0,"Help_txt.",0
SAVE_T1 DB	"Confirm Save 128Kb-State Into 512Kb-Pos.?! (yes/no)"
        DB	0
LOAD_T1 DB	"Confirm Load 128Kb-State From 512Kb-Pos.?! (yes/no)"
        DB	0
EXIT_T1 DB	"Confirm Escaping Into Current 128Kb-State. (yes/no)"
        DB	0
STS_T1  DB	"Confirm Call Stalker's Monitor (STS4.3i).  (yes/no)"
        DB	0

INFIN_T DB	"Select Infinitiser Analyze Mode & Confirm! (yes/no)"
        DB	0
        DB	"Pos.1?Pos.2?128K.   1 - Changes.  4 - Subtraction. "
        DB	0
        DB	"Pos.1=Pos.2?128K.   2 - Decrease. 5 - Addition.    "
        DB	0
        DB	"Pos.1?Pos.2.        3 - Increase. 0 - *Mode Select."
        DB	0
INFA_PT DB	"Searching: ",0
INFA_TY DB	"ADDR.:P.1>P.2>128,Symbols",0
INFA_BT DB	"Bank ",0
INFA_ET DB	"End of List. ",0
LINK1_T DB	"A.(",0
LINK2_T DB	") Link N",0XF8,0
LINK3_T DB	") No Links.  ",0

TRDOS_T DB	"Insert Disk(A>), Select Options & Confrim! (yes/no)"
        DB	0
        DB	"1 - Disk Save. 4 - 128Kb-State.  7 - For All 128Kb."
        DB	0
TRDOST2 DB	"2 - Disk Load. 5 - Position 1.   8 - For 48Kb Only."
        DB	0
TRDOST3 DB	"3 - Make Slot. 6 - Position 2.   0 - Change Drive. "
        DB	0
TRCAT_T DB	"Reading Catalogue (Do Not Remove Disk From Drive!)."
        DB	0
TRERR_T DB	"<< Disk Error >> (No Disk Maybe?): Retry?! (yes/no)"
        DB	0
NOSLT_T DB	"There is no any Slot in this Disk. Make Slot? (y/n)"
        DB	0
YESLT_T DB	"Select Slot For Disk Operation (Don't Change Disk)."
        DB	0
ERSLT_T DB	"Can't      128Kb      48Kb Slot. Reselect? (yes/no)"
        DB	0
MKSLT_T DB	"Input Slot-File Name: [        .   ] & Press Enter."
        DB	0
EFRSC_T DB	"Not Enough Free Sectors To Create New Slot. (any k)"
        DB	0
EFRFL_T DB	"Not Enough Free Files To Create New Slot. (any key)"
        DB	0
ECRPT_T DB	"This Disk Has Some Inner Non-Consistancy. (any key)"
        DB	0
TRCAT2T DB	"Writing Catalogue (Do Not Remove Disk From Drive!)."
        DB	0

UTIL_PT DB	"Searching Util.: ",0
NO_UT_T DB	"There is no any Utilite in Memory.  (press any key)"
        DB	0
UTIL_T  DB	"Select Utilite For CALL or RUN & Confirm!  (yes/no)"
        DB	0

HELP_T  DB	"Confirm Call Help_txt Viewer.              (yes/no)"
        DB	0
HELP_T1 DB	"Depacking...",0
HELP_T2 DB	"There is no Help_txt Viewer in Memory!  (press 'n')"
        DB	0

;------
;TRDOS;
;------

		include trdos.a80	;INCLUDE	"TRDOS.H",0x43

;---
