
;LAST UPDATE: 23.02.2010 savelij

;ОБЩАЯ ТОЧКА ВХОДА ДЛЯ РАБОТЫ С HDD SMUC
        EXA:EX (SP),HL:LD A,(HL):INC HL
        EX (SP),HL:ADD A,A:PUSH HL
        LD HL,TBLHDDS:ADD A,L:LD L,A,A,H
        ADC A,0:LD H,A,A,(HL):INC HL
        LD H,(HL),L,A:EXA:EX (SP),HL:RET

TBLHDDS DW HDSINIT
        DW HDSOFF
        DW HDSRDS;READ SINGLE
        DW HDSRDM;READ MULTI

;Входные параметры общие:
;HL-адрес загрузки в память
;BCDE-32-х битный номер сектора
;A-количество блоков (блок=512 байт)
;только для многоблочной записи/чтении

PS1F7   EQU #FF;#FFBE
;РЕГИСТР СОСТОЯНИЯ/РЕГИСТР КОМАНД
PS1F6   EQU #FE;#FEBE
;CHS-НОМЕР ГОЛОВЫ И УСТР/LBA АДРЕС 24-27
PS1F5   EQU #FD;#FDBE
;CHS-ЦИЛИНДР 8-15/LBA АДРЕС 16-23
PS1F4   EQU #FC;#FCBE
;CHS-ЦИЛИНДР 0-7/LBA АДРЕС 8-15
PS1F3   EQU #FB;#FBBE
;CHS-НОМЕР СЕКТОРА/LBA АДРЕС 0-7
PS1F2   EQU #FA;#FABE
;СЧЕТЧИК СЕКТОРОВ
PS1F1   EQU #F9;#F9BE
;ПОРТ ОШИБОК/СВОЙСТВ
PS1F0   EQU #F8;#F8BE
;ПОРТ ДАННЫХ
PS3F6   EQU #FE;#FEBE
;РЕГИСТР СОСТОЯНИЯ/УПРАВЛЕНИЯ
PSHI    EQU #D8;#D8BE
;СТАРШИЕ 8 БИТ
PRTSRW  EQU PS1F0*256+PSHI
;ПОРТЫ ЧТЕНИЯ/ЗАПИСИ ОДНИМ СЛОВОМ
LOW_PRT EQU #BE
;МЛАДШИЙ БАЙТ АДРЕСА SMUC IDE
SMUCSYS EQU #FFBA
;СИСТЕМНЫЙ ПОРТ SMUC
SMUCVER EQU #5FBA
;ПОРТ ВЕРСИИ SMUC

;НА ВЫХОДЕ:
;H-ДЛЯ MASTER 0-HDD, 1-CDROM, #FF-NONE
;L-ДЛЯ SLAVE  0-HDD, 1-CDROM, #FF-NONE
HDSINIT PUSH HL:EX DE,HL:LD HL,#3FF0
        LD BC,6:CALL LDIRRET:POP HL
        LD B,6,DE,CPINOUT
HDSINI1 LD A,(DE):CP (HL):INC DE,HL
        JR NZ,HDSINI2:DJNZ HDSINI1
        LD BC,SMUCVER:DEC SP,SP
        POP HL:CALL SINPRT:INC A
        JR NZ,HDSINI0

HDSINI2 LD HL,#FFFF:XOR A:RET

HDSINI0 LD BC,SMUCSYS,A,#77:CALL SOUTPRT
        PUSH HL:LD A,#E0:CALL IDSDEV
        POP HL:AND A:CALL Z,INITS91
        LD D,A,A,#F0:PUSH DE,HL
        CALL IDSDEV:POP HL:AND A
        CALL Z,INITS91:POP HL:LD L,A
        XOR A

HDSOFF  RET

INITS91 PUSH HL:LD L,49*2+1,A,(HL):AND 2
        JR Z,INIS912
        LD BC,(PS1F2*#0100)+LOW_PRT
        LD L,#0C,A,(HL):CALL SOUTPRT
        LD L,6,B,PS1F6,A,(HL):DEC A
        CALL SOUTPRT:LD B,PS1F7,A,#91
        CALL SOUTPRT:LD DE,#1000
INIS911 DEC DE:LD A,D:OR E:JR Z,INIS912
        CALL SINPRT:AND #80
        JR NZ,INIS911:POP HL:RET

INIS912 LD A,#FF:POP HL:RET

HDSRDS  LD A,1
HDSRDM  PUSH BC,DE:CALL SETSREG:EXA
        LD B,PS1F7,A,#20:CALL SOUTPRT
        LD B,PS1F7
HDSRDM2 CALL SINPRT:AND #88:CP 8
        JR NZ,HDSRDM2:EXA
HDSRDM1 PUSH AF:CALL RDCSSEC:LD B,PS1F7
HDSRDM3 CALL SINPRT:AND #80
        JR NZ,HDSRDM3:POP AF
        DEC A:JR NZ,HDSRDM1
	POP DE,BC:LD A,B:AND #0F:LD B,A
        XOR A:RET

RDCSSEC LD DE,PRTSRW,A,#40
RDCSSC1 EXA
	DUP 4
        LD B,D:CALL SINPRT:LD (HL),A
        INC HL:LD B,E:CALL SINPRT
        LD (HL),A:INC HL
        EDUP
        EXA:DEC A:JR NZ,RDCSSC1:RET

SETSREG PUSH DE:LD DE,BC:EXA
        LD BC,(PS1F6*#0100)+LOW_PRT,A,D
        CALL SOUTPRT
        LD B,PS1F5,A,E:CALL SOUTPRT
        POP DE:LD B,PS1F4,A,D
        CALL SOUTPRT:LD B,PS1F3:LD A,E
        CALL SOUTPRT:LD B,PS1F2:EXA

SOUTPRT PUSH HL:LD HL,#3FF0:EX (SP),HL
        JP #3D2F

LDIRRET PUSH HL:LD HL,#180D:EX (SP),HL
        JP #3D2F

CPINOUT OUT (C),A:RET:IN A,(C):RET

SINPRT  PUSH HL:LD HL,#3FF3:EX (SP),HL
        JP #3D2F

;HL-АДРЕС БУФЕРА СЕКТОРА ИДЕНТИФИКАЦИИ
;A=E0-ДЛЯ MASTER, A=F0-ДЛЯ SLAVE
IDSDEV  LD BC,(PS1F6*#0100)+LOW_PRT
        CALL SOUTPRT:LD B,PS1F7,D,26
IDSDEV2 EI:HALT:DI:DEC D:JR Z,NOSDEV
        CALL SINPRT:BIT 7,A
        JR NZ,IDSDEV2:AND A:JR Z,NOSDEV
        INC A:JR Z,NOSDEV:XOR A
        LD B,PS1F5:CALL SOUTPRT
        LD B,PS1F4:CALL SOUTPRT
        LD A,#EC,B,PS1F7:CALL SOUTPRT
        LD B,PS1F7
ISDEV3  CALL SINPRT:AND A:JR Z,NOSDEV
        INC A:JR Z,NOSDEV:DEC A:RRCA
        JR C,IDSDEV1:RLCA:AND #88
        CP 8:JR NZ,ISDEV3
IDSDEV1 LD B,PS1F4:CALL SINPRT
        LD E,A,B,PS1F5:CALL SINPRT
        LD D,A:OR E:JP Z,RDCSSEC
        LD HL,#EB14,A,1:SBC HL,DE:RET Z
NOSDEV  LD A,#FF:RET
