
;LAST UPDATE: 12.02.2010 savelij

;мнлепю бшгшбюелшу тсмйжхи
Wc_fat		EQU 0X00		;хмхжхюкхгюжхъ оепелеммшу
Getfzap		EQU 0X01		;онксвемхе нохяюрекъ тюикю/дхпейрнпхх
Ent_dir		EQU 0X02		;бунд б дхпс/бшунд хг дхпш
Openfil		EQU 0X03		;нрйпшрхе тюикю
Readfil		EQU 0X04		;времхе нрйпшрнцн тюикю
Getlong		EQU 0X05		;онксвемхе пюяоюйнбюммнцн дкхммнцн хлемх
Positf		EQU 0X06		;пюанрю я онгхжхеи рейсыецн тюикю
Prvlegz		EQU 0X07		;онхяй опедшдсыеи "кецюкэмни гюохях"
Nxtlegz		EQU 0X08		;онхяй якедсчыеи "кецюкэмни гюохях"
Fhobeta		EQU 0X09		;онхяй HOBETA тюикнб я опнбепйни

;оепелеммше FAT
BUF_512		EQU DRV_VAR		;0X200 астеп яейрнпю
TDIRCLS		EQU BUF_512+0X0200	;0X400 рюакхжю йкюярепнб DIR
DIR_CEP		EQU TDIRCLS+0X0400	;0X100 жеонвйю йкюярепнб онддхпейрнпхи
ERORDRV		EQU DIR_CEP+0X0100	;2 бнгбпюр я ньхайни б A
CAL_FAT		EQU ERORDRV+2		;1 йюкхап FAT
MANYFAT		EQU CAL_FAT+1		;1 йнкхвеярбн FAT-рюакхж
BYTSSEC		EQU MANYFAT+1		;1 йнкхвеярбн яейрнпнб б йкюярепе
ROOTCLS		EQU BYTSSEC+1		;4 яейрнп мювюкю ROOT дхпейрнпхх
ROOTZAP		EQU ROOTCLS+4		;2 йнк-бн гюохяеи б ROOT дхпейрнпхх
SEC_FAT		EQU ROOTZAP+2		;4 йнкхвеярбн яейрнпнб ндмни тюр
RSVDSEC		EQU SEC_FAT+4		;2 пюглеп пегепбмни накюярх
STARTRZ		EQU RSVDSEC+2		;4 мювюкн дхяйю/пюгдекю
FRSTDAT		EQU STARTRZ+4		;4 юдпея оепбнцн яейрнпю дюммшу нр BPB
SEC_DSC		EQU FRSTDAT+4		;4 йнкхвеярбн яейрнпнб мю дхяйе/пюгдеке
CLS_DSC		EQU SEC_DSC+4		;4 йнкхвеярбн йкюярепнб мю дхяйе/пюгдеке
FATSTR		EQU CLS_DSC+4		;4 мювюкн оепбни FAT рюакхжш
FILE_SZ		EQU FATSTR+4		;4 пюглеп тюикю б аюирюу
KOLWCLS		EQU FILE_SZ+4		;4 йнкхвеярбн ябнандмшу йкюярепнб
LST0ZAP		EQU KOLWCLS+4		;4 оепбши ябнандмши йкюяреп
LSTLOAD		EQU LST0ZAP+4		;4 мнлеп яейрнпю гюцпсфеммнцн б астеп
TEK_DIR		EQU LSTLOAD+4		;4 йкюяреп рейсыеи DIR
TEK_ZAP		EQU TEK_DIR+4		;2 рейсыюъ гюохяэ б DIR
SAVEZAP		EQU TEK_ZAP+2		;2 янупюмеммюъ рейсыюъ онгхжхъ
TEK_LVL		EQU SAVEZAP+2		;1 рейсыхи спнбемэ йюрюкнцю
TEK_LEV		EQU TEK_LVL+1		;1 цксахмю мюунфдемхъ б дхпейрнпхъу
FREECLS		EQU TEK_LEV+1		;8 янупюмемше оепелеммше дкъ нрйюрю
KOLFIND		EQU FREECLS+8		;2 йнкхвеярбн мюидеммшу он люяйе тюикнб
AFILCLS		EQU KOLFIND+2		;4 мнлеп оепбнцн йкюярепю дкъ гюцпсгйх
TFILCLS		EQU ERORDRV+0X0100	;? рюакхжю йкюярепнб тюикю, пюглеп гюбхяхр нр пюглепю йкюярепю

;наыюъ рнвйю бундю дкъ пюанрш я FAT
		EX AF,AF'
		EX (SP),HL
		LD A,(HL)
		INC HL
		EX (SP),HL
		ADD A,A
		PUSH IX
		PUSH IY
		LD (ERORDRV),SP
		PUSH HL
		LD HL,EXITDRV
		EX (SP),HL
		PUSH HL
		LD HL,TABLFAT
		ADD A,L
		LD L,A
		LD A,H
		ADC A,0
		LD H,A
		LD A,(HL)
		INC HL
		LD H,(HL)
		LD L,A
		EX AF,AF'
		EX (SP),HL
		RET

ERR_DRV		LD SP,(ERORDRV)
EXITDRV		POP IY
		POP IX
		RET

TABLFAT		DW WC_FAT		;00 INIT FAT&SD/HDD
		DW GETFZAP		;01 HL=FILE OPISATEL
		DW ENT_DIR		;02 ENTER DIR
		DW OPENFIL		;03 OPENFILE
		DW READFIL		;04 READ FILE
		DW GETLONG		;05 GET LONGNAME
		DW POSITF		;06 WORK FOR TEK POSIT
		DW PRVLEGZ		;07 PREV LEGAL OPISAT
		DW NXTLEGZ		;08 NEXT LEGAL OPISAT
		DW FHOBETA		;09 онхяй тюикнб он пюяьхпемхч

;времхе/гюохяэ я тюик я рнвмнярэч
;дн яейрнпю (512 аюир)
READFIL		EXX
		LD E,0
		EXX
;		JR RD_ADD

;ADD2FIL	EXX
;		LD E,1
;		EXX
RD_ADD		PUSH AF
		PUSH HL
		EX AF,AF'
		LD A,(BYTSSEC)
		LD H,A
		PUSH DE
		CALL BCDE_A
		LD C,H
		LD HL,TFILCLS
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		EX DE,HL
		POP HL
		LD A,C
		LD B,0
		DEC A
		AND L
		JR Z,ADD2FI3-5
		EXX
		LD C,A
		LD B,0
		EXX
		LD L,A
		LD A,C
		SUB L
		LD H,A
		EX AF,AF'
		CP H
		JR C,$+3
		LD A,H
		POP HL
		PUSH HL
		CALL ADD2FI2+1
		LD B,C
		LD A,(BYTSSEC)
		LD C,A
		EX (SP),HL
		EXX
		LD BC,0
		EXX
ADD2FI3		POP HL
		POP AF
		SUB B
		RET Z
		LD B,0
		CP C
		JR Z,ADD2FI2+1
		INC B
		SUB C
		JR NC,$-2
		DEC B
		ADD A,C
		PUSH AF
		LD A,B
		AND A
		JR Z,ADD2FI2
		POP AF
ADD2FI1		PUSH AF
		PUSH BC
		PUSH HL
		LD A,C
		EX DE,HL
		CALL LOADZP
		BIT 7,B
		JP NZ,ERR_DRV
		EX (SP),HL
		PUSH HL
		PUSH AF
		CALL REALSEC
		EXX
		PUSH BC
		EXX
		POP HL
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
		POP AF
		POP HL
		EX AF,AF'
		EXX
		LD A,E
		EXX
		AND A
		JR Z,ADD2FI5
		EX AF,AF'
		CALL TO_DRV
		DB 5
		JR ADD2FI4

ADD2FI5		EX AF,AF'
		CALL TO_DRV
		DB 3
ADD2FI4		AND A
		JP NZ,ERR_DRV
		POP DE
		POP BC
		DJNZ ADD2FI1+1
ADD2FI2		POP AF
		INC B
		AND A
		RET Z
		LD C,A
		XOR A
		JR ADD2FI1

;нрйпшрхе тюикю
OPENFIL		CALL GETFZAP
		LD DE,20
		ADD HL,DE
		LD C,(HL)
		INC HL
		LD B,(HL)
		LD E,5
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		INC HL
		PUSH BC
		PUSH DE
		CALL LOADZP
		LD (FILE_SZ),DE
		LD (FILE_SZ+2),BC
		POP DE
		POP BC
		LD HL,TFILCLS
OPENFI1		CALL SAVEZP
		PUSH HL
		CALL RDFATZP
		CALL LST_CLS
		POP HL
		JR NC,OPENFI1
		LD B,0XFF
		CALL SAVEZP
FILESEC		LD DE,(FILE_SZ)
		LD BC,(FILE_SZ+2)
		CALL HWOSTSC
		JP BCDE200

HWOSTSC		LD A,D
		AND 1
		LD H,A
		LD A,E
		AND A
		JR Z,$+3
		INC H
		LD E,0
		LD L,E
		BIT 0,H
		JR Z,$+3
		INC H
		RES 0,D
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
		RET

;гюцпсфюел яейрнп б астеп
LOADLST		CALL CPNUMSC
		JR NZ,LOADLS1
		LD HL,BUF_512
		RET

LOADLS1		LD (LSTLOAD+2),BC
		LD (LSTLOAD),DE
		LD HL,BUF_512
		PUSH HL
		CALL TO_DRV
		DB 2
		AND A
		JP NZ,ERR_DRV
		POP HL
		RET

;опнбепйю мю сфе гюцпсфеммши яейрнп
CPNUMSC		LD HL,LSTLOAD
		LD A,(HL)
		INC HL
		CP E
		RET NZ
		LD A,(HL)
		INC HL
		CP D
		RET NZ
		LD A,(HL)
		INC HL
		CP C
		RET NZ
		LD A,(HL)
		CP B
		RET

;бунфдемхе б DIR
ENT_DIR		CALL GETFZAP
		BIT 4,A
		EX AF,AF'
		LD A,E
		EX AF,AF'
		LD E,A
		LD D,(HL)
		RET Z
		EXX
		EX AF,AF'
		BIT 7,A
		JR Z,ENT_DI2
		LD HL,TEK_LEV
		LD A,(HL)
		AND A
		EXX
		RET Z
		EXX
		DEC A
		LD (HL),A
		ADD A,A
		LD H,HIGH (DIR_CEP)
		LD L,A
		LD A,(HL)
		INC L
		LD H,(HL)
		LD L,A
		JR ENT_DI1

ENT_DI2		LD HL,TEK_LEV
		LD A,(HL)
		INC A
		CP 0X80
		EXX
		RET Z
		EXX
		LD (HL),A
		DEC A
		ADD A,A
		LD H,HIGH (DIR_CEP)
		LD L,A
		LD BC,(TEK_ZAP)
		LD (HL),C
		INC L
		LD (HL),B
ENT_DI1		EXX
		PUSH DE
		LD DE,20
		ADD HL,DE
		LD C,(HL)
		INC HL
		LD B,(HL)
		LD E,5
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		LD A,B
		OR C
		OR D
		OR E
		JR NZ,ENT_DI0
		LD DE,(ROOTCLS)
		LD BC,(ROOTCLS+2)
ENT_DI0		LD (TEK_DIR),DE
		LD (TEK_DIR+2),BC
		CALL INIRTSC
		POP DE
		RET

;онксвхрэ нохяюрекэ рейсыецн тюикю
GETFZAP		LD BC,(TEK_ZAP)			;рейсыхи мнлеп нохяюрекъ
		LD A,B
		OR C
		JR Z,GETRZAP			;еякх мнлеп =0, рн опедшдсыхи мнлеп ме опнбепъел
		PUSH BC				;янупюмхкх рейсыхи мнлеп
		DEC BC				;мсфмн опнбепхрэ опедшдсыхи мнлеп
		CALL GETRZAP			;опнбепъел
		POP BC				;бепмскх рейсыхи мнлеп
		LD E,A				;хг опедшдсыецн нохяюрекъ мсфем рнкэйн ткюцнбши аюир
GETRZAP		PUSH DE
		CALL RDDIRSC			;вхрюел яейрнп я рейсыхл мнлепнл
		POP DE				;бепмскх пецхярп E
		INC A
		LD A,E				;бепмскх мюкхвхе/нрясярбхе дкхммнцн хлемх
		RET Z				;бшундхл еякх бшкерекх гю опедекш ясыеярбсчыеи дхпш
		EX AF,AF'			
GETFZA1		LD A,C
		AND 0X0F			;онксвхкх мнлеп нохяюрекъ б гюцпсфеммнл яейрнпе дхпш
		LD E,A
		LD D,0
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE			;бшвхякхкх юдпея бшапюммнцн нохяюрекъ
		LD DE,0X0B
		LD A,(HL)			;бгъкх ткюцнбши аюир рейсыецн нохяюрекъ
		EX DE,HL
		ADD HL,DE
		LD L,(HL)			;бгъкх оепбши аюир хлемх
		LD H,A				;онкнфхкх ткюцнбши аюир щрнцн нохяюрекъ
		EX DE,HL
		CALL CP_TWOT			;опнбепйю мю мюкхвхе б хлемх ".." 
		LD D,0				;асдел бундхрэ б дхпейрнпхч
		JR NZ,GETFZA2
		LD D,0X80			;асдел бшундхрэ б опедшдсысч дхпейрнпхч
GETFZA2		EX AF,AF'
		CP 0X0F				;опнбепъел ткюцнбши аюир опедшдсыецн нохяюрекъ
		LD A,E				;бгъкх ткюцнбши аюир рейсыецн нохяюрекъ
		LD E,D				;бшярюбхкх мюопюбкемхе дбхфемхъ б дхпюу
		RET NZ
		INC E				;сярюмнбйю мюкхвхъ дкхммнцн хлемх
		RET

;времхе яейрнпю DIR он мнлепс BC
;мю бшунде: A=0XFF-бшунд гю опедекш DIR
RDDIRSC		PUSH BC
		LD D,B
		LD E,C
		LD BC,0
		LD A,0X10
		CALL BCDE_A
		LD A,E
		PUSH AF
		LD A,(BYTSSEC)
		PUSH AF
		CALL BCDE_A
		LD HL,TDIRCLS
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		CALL LOADZP
		BIT 7,B
		JR Z,RDDIRS3
		POP BC
		POP BC
		POP BC
		XOR A
		DEC A
		RET

RDDIRS3		CALL REALSEC
		POP AF
		DEC A
		LD L,A
		POP AF
		AND L
		LD L,A
		LD H,0
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
		CALL LOADLST
		POP BC
		XOR A
		RET

;тнплхпнбюмхе рюакхжш йкюярепнб DIR
;х онхяй оепбни "кецюкэмни" гюохях б DIR
INIRTSC		PUSH AF
		LD HL,TDIRCLS
		CALL SAVEZP
		LD A,D
		OR E
		OR B
		OR C
		JR Z,LASTCLS
NEXTCLS		PUSH HL
		CALL RDFATZP
		CALL LST_CLS
		POP HL
		JR C,LASTCLS
		CALL SAVEZP
		JR NEXTCLS

LASTCLS		LD BC,0XFFFF
		CALL SAVEZP
		CALL POSTF02
		POP AF
		RET

;опнбепйю мю 1 рнвйс
CP_ONET		LD A,(HL)
		CP 0X2E
		RET NZ
		INC HL
		LD A,(HL)
		DEC HL
		CP 0X20
		RET NZ
		XOR A
		RET

;опнбепйю мю 2 рнвйх
CP_TWOT		LD A,(HL)
		CP 0X2E
		RET NZ
		INC HL
		LD A,(HL)
		DEC HL
		CP 0X2E
		RET NZ
		XOR A
		RET

;PAGEMEM		PUSH BC
;		OR 0X10
;		LD BC,0X7FFD
;		OUT (C),A
;		POP BC
;		RET

POSITF		CP LOW ((ENDTPOS-TPOSITF)/2)
		RET NC
		ADD A,A
		LD DE,TPOSITF
		LD L,A
		LD H,0
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		EX DE,HL
		JP (HL)

TPOSITF		DW POSTF00
		DW POSTF01
		DW POSTF02
		DW POSTF03
		DW POSTF04
		DW POSTF05
		DW POSTF06
		DW POSTF07
ENDTPOS

;янупюмемхе рейсыеи онгхжхх тюикю
POSTF00		LD HL,(TEK_ZAP)
		LD (SAVEZAP),HL
		RET

;бняярюмнбкемхе рейсыеи онгхжхх тюикю
POSTF01		LD HL,(SAVEZAP)
		LD (TEK_ZAP),HL
		RET

;оепелнрюрэ мю "B" тюикнб мюгюд
POSTF03		LD A,B
		AND A
		RET Z
		PUSH BC
		CALL PRVLEGZ
		POP BC
		DJNZ $-5
		RET

;оепелнрюрэ мю "B" тюикнб боепед
POSTF04		LD A,B
		AND A
		RET Z
		PUSH BC
		CALL NXTLEGZ
		POP BC
		DJNZ $-5
		RET

;ондявер йнкхвеярбю "кецюкэмшу" гюохяеи
POSTF05		LD BC,0
		PUSH BC
		EXX
		POP BC
		EXX
		CALL RDDIRSC
		LD DE,0X20
KOL_ZA4		LD A,(HL)
		CP 0X2E
		JR NZ,KOL_ZA1+2
		INC HL
		LD A,(HL)
		DEC HL
		CP 0X2E
		JR Z,KOL_ZA1+2
		INC BC
		ADD HL,DE
		JR KOL_ZA4

KOL_ZA1		INC BC
		ADD HL,DE
		LD A,H
		CP HIGH (BUF_512)+2
		JR NZ,KOL_ZA2
		CALL RDDIRSC
		LD D,0
		AND A
		JR Z,KOL_ZA2
		CALL KOL_ZA3
		DEC A
		RET

KOL_ZA2		LD E,0X0B
		ADD HL,DE
		LD A,(HL)
		AND A
		SBC HL,DE
		LD E,0X20
		CP 0X0F
		JR Z,KOL_ZA1
		CP 8
		JR Z,KOL_ZA1
		LD A,(HL)
		CP 0XE5
		JR Z,KOL_ZA1
		AND A
		JR Z,KOL_ZA3
		EXX
		INC BC
		EXX
		JR KOL_ZA1

KOL_ZA3		LD D,B
		LD E,C
		EXX
		PUSH BC
		EXX
		POP BC
		XOR A
		RET

;сярюмнбхрэ мнлеп "кецюкэмни" гюохях хг "BC"
POSTF06		LD (TEK_ZAP),BC
		RET

;бепмсрэ б "BC" рейсыхи мнлеп "кецюкэмни" гюохях
POSTF07		LD BC,(TEK_ZAP)
		RET

CPTEKZP		CALL RDDIRSC
		RET M
		LD A,C
		AND 0X0F
		LD L,A
		LD H,0
		LD DE,BUF_512
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		LD DE,0X0B
		ADD HL,DE
		LD A,(HL)
		SBC HL,DE
		LD E,(HL)
		AND A
		RET

;яапня рейсыеи онгхжхх б 0 х онхяй оепбни "кецюкэмни" гюохях
POSTF02		LD BC,0
		LD (TEK_ZAP),BC
		CALL RDDIRSC
		CALL CP_ONET
		JR Z,NXTLEG1
		LD A,(HL)
		AND A
		RET Z
		JR NXTLEG1+1

NXTLEGZ		LD BC,(TEK_ZAP)
NXTLEG1		INC BC
		CALL CPTEKZP
		CP 0XFF
		RET Z
		CP 0X0F
		JR Z,NXTLEG1
		CP 8
		JR Z,NXTLEG1
		LD A,E
		AND A
		RET Z
		CP 0XE5
		JR Z,NXTLEG1
		LD (TEK_ZAP),BC
		RET

PRVLEGZ		LD BC,(TEK_ZAP)
		CALL CPTEKZP
		CALL CP_TWOT
		RET Z
PRVLEG1		DEC BC
		CALL CPTEKZP
		CP 0X0F
		JR Z,PRVLEG1
		CP 8
		JR Z,PRVLEG1
		LD A,E
		CP 0XE5
		JR Z,PRVLEG1
		LD (TEK_ZAP),BC
		RET

SAVEZP		LD (HL),E
		INC HL
		LD (HL),D
		INC HL
		LD (HL),C
		INC HL
		LD (HL),B
		INC HL
		RET

LOADZP		LD E,(HL)
		INC HL
		LD D,(HL)
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
		INC HL
		RET

;хмхжхюкхгюжхъ оепелеммшу FAT
WC_FAT		LD HL,0XFFFF
		LD (LSTLOAD),HL
		LD (LSTLOAD+2),HL
		CALL COM_DEV
		DB 2
		LD (CAL_FAT),A
		LD E,D
		LD D,0
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		INC HL
		INC HL
		INC HL
		LD E,(HL)
		INC HL
		LD D,(HL)
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
		LD (STARTRZ),DE
		LD (STARTRZ+2),BC
		CALL LOADLST
		PUSH HL
		POP IX
		LD HL,0
		LD E,(IX+0X16)
		LD D,(IX+0X17)		;BPB_FATSZ16
		LD A,D
		OR E
		JR NZ,RDFAT01		;еякх ме FAT12/16 (BPB_FATSZ16=0)
		LD E,(IX+0X24)
		LD D,(IX+0X25)
		LD L,(IX+0X26)
		LD H,(IX+0X27)		;BPB_FATSZ32
					;рн аепел хг ялеыемхъ +36
RDFAT01		LD (SEC_FAT+2),HL
		LD (SEC_FAT),DE		;вхякн яейрнпнб мю FAT-рюакхжс
		LD HL,0
		LD E,(IX+0X13)
		LD D,(IX+0X14)		;BPB_TOTSEC16
		LD A,D
		OR E
		JR NZ,RDFAT02		;еякх ме FAT12/16 (BPB_TOTSEC16=0)
		LD E,(IX+0X20)
		LD D,(IX+0X21)
		LD L,(IX+0X22)
		LD H,(IX+0X23)		;BPB_TOTSEC32
					;рн аепел хг ялеыемхъ +32
RDFAT02		LD (SEC_DSC+2),HL
		LD (SEC_DSC),DE		;й-бн яейрнпнб мю дхяйе/пюгдеке

;бшвхякъел ROOTDIRSECTORS
		LD E,(IX+0X11)
		LD D,(IX+0X12)		;BPB_ROOTENTCNT
		LD (ROOTZAP),DE
		LD BC,0
		LD H,B
		LD L,C
		LD A,D
		OR E
		JR Z,RDFAT03
		LD A,0X10
		CALL BCDE_A
		EX DE,HL

;щрн пеюкхгнбюмю тнплскю
;ROOTDIRSECTORS=((BPB_ROOTENTCNT*32)++(BPB_BYTSPERSEC-1))/BPB_BYTSPERSEC
;б HL ROOTDIRSECTORS. еякх FAT32, рн HL=0 бяецдю

RDFAT03		PUSH HL			;ROOTDIRSECTORS
		LD A,(IX+0X10)		;BPB_NUMFATS
		LD (MANYFAT),A
		LD DE,(SEC_FAT)
		LD HL,(SEC_FAT+2)
		DEC A
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		DEC A
		JR NZ,$-6
		POP BC			;онкмши пюглеп FAT-накюярх б яейрнпюу
		CALL HLDEPBC		;опхаюбхкх ROOTDIRSECTORS
		LD C,(IX+0X0E)
		LD B,(IX+0X0F)		;BPB_RSVDSECCNT
		LD (RSVDSEC),BC
		CALL HLDEPBC		;опхаюбхкх BPB_RESVDSECCNT
		LD (FRSTDAT),DE
		LD (FRSTDAT+2),HL	;онкнфхкх мнлеп оепбнцн яейрнпю дюммшу
		LD B,H
		LD C,L
		LD HL,SEC_DSC		;BCDE+32-НЕ вхякн он юдпеяс HL
		CALL BCDEHLM		;бшвкх хг онкмнцн й-бю яейрнпнб пюгдекю
		LD A,(IX+0X0D)
		LD (BYTSSEC),A
		CALL BCDE_A		;пюгдекхкх мю й-бн яейрнпнб б йкюярепе
		LD (CLS_DSC),DE
		LD (CLS_DSC+2),BC	;онкнфхкх йнк-бн йкюярепнб мю пюгдеке

		LD A,(CAL_FAT)
		CP 0XFF
		JR NZ,RDFAT04
		LD HL,(CLS_DSC)
		LD DE,(CLS_DSC+2)
		PUSH HL
		PUSH DE
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		LD B,H
		LD C,L
		CALL RASCHET
		LD A,1
		POP DE
		POP HL
		JR Z,RDFAT04
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		LD B,H
		LD C,L
		CALL RASCHET
		LD A,2
		JR Z,RDFAT04
		XOR A

RDFAT04		LD (CAL_FAT),A

;дкъ FAT12/16 ROOT йкюяреп=0
;дкъ FAT32 аепел он ялеыелхч +44
;мю бшунде BCDE-яейрнп ROOTDIR йкюяреп
		PUSH AF
		AND A
		LD DE,0
		LD B,D
		LD C,E
		JR Z,FSRROO2		;FAT12
		DEC A
		JR Z,FSRROO2		;FAT16
		LD E,(IX+0X2C)
		LD D,(IX+0X2D)
		LD C,(IX+0X2E)
		LD B,(IX+0X2F)		;FAT32
FSRROO2		LD (ROOTCLS),DE
		LD (ROOTCLS+2),BC	;яейрнп ROOT дхпейрнпхх
		LD (TEK_DIR),DE
		LD (TEK_DIR+2),BC
FSRR121		PUSH DE
		PUSH BC
		LD DE,(RSVDSEC)
		LD BC,0
		LD HL,STARTRZ
		CALL BCDEHLP
		LD (FATSTR),DE
		LD (FATSTR+2),BC
		XOR A
		LD (TEK_LEV),A
		POP BC
		POP DE
		POP AF
		JP INIRTSC

RASCHET		CALL BCDE200
		LD HL,SEC_FAT
		CALL BCDEHLM
		LD A,E
		AND 0XF0
		OR D
		OR C
		OR B
		RET

NO_LNG		POP DE
		LD BC,8
		LDIR
		LD A,(HL)
		CP 0X20
		JR Z,NO_LNG1
		EX DE,HL
		LD (HL),0X2E
		INC HL
		EX DE,HL
		LDI
		LDI
		LDI
NO_LNG1		EX DE,HL
		LD (HL),0
		POP HL
		RET

GETLONG		PUSH HL
		PUSH HL
		CALL GETFZAP
		BIT 0,E
		JR Z,NO_LNG
		EXX
		LD C,0
		EXX
GETLNG2		DEC BC
		CALL GETRZAP
		LD A,(HL)
		INC HL
		EXX
		BIT 6,C
		POP HL
		JR NZ,GETLNGE+1
		LD C,A
		EXX
		LD A,5
		CALL GETLNG1
		AND A
		JR Z,GETLNGE
		INC HL
		INC HL
		INC HL
		LD A,6
		CALL GETLNG1
		AND A
		JR Z,GETLNGE
		INC HL
		INC HL
		LD A,2
		CALL GETLNG1
		AND A
		JR Z,GETLNGE
		EXX
		PUSH HL
		EXX
		JR GETLNG2

GETLNGE		EXX
		LD (HL),0
		EXX
		POP HL
		RET

GETLNG1		EXX
		LD B,A
		EXX
		LD D,(HL)
		INC HL
		LD E,(HL)
		INC HL
		LD A,D
		OR E
		RET Z
		LD A,E
		AND A
		JR NZ,GETLNG3
		LD A,D
		CP 0X80
		JR C,GETLNG5
		LD D,0X5F
		JR GETLNG5

GETLNG3		CP 4
		LD A,0X5F
		JR NZ,GETLNG5
		LD A,D
		LD E,0XEF
		LD D,0X5F
		CP 1
		JR Z,GETLNG4
		LD E,0XA0
		CP 0X51
		JR Z,GETLNG4
		SUB 0X10
		LD E,0X80
		JR NC,GETLNG6
		LD A,D
		JR GETLNG5

GETLNG6		CP 0X30
		JR C,GETLNG4
		LD E,0XB0
		CP 0X40
		JR C,GETLNG4
		LD A,D
		JR GETLNG5

GETLNG4		ADD A,E
GETLNG5		EXX
		LD (HL),A
		INC HL
		DEC B
		EXX
		RET Z
		JR GETLNG1+3
;401,410-44F,451

LST_CLS		LD A,(CAL_FAT)
		AND A
		JR NZ,LST_CL1
		LD HL,0X0FF7
		SBC HL,DE
		RET

LST_CL1		DEC A
		JR NZ,LST_CL2
		LD HL,0XFFF7
		SBC HL,DE
		RET

LST_CL2		LD HL,0X0FFF
		SBC HL,BC
		RET NZ
		LD HL,0XFFF7
		SBC HL,DE
		RET

RDFATZP		LD A,(CAL_FAT)
		AND A
		JR Z,RDFATS0
		DEC A
		JR Z,RDFATS1
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		LD HL,0
		ADC HL,BC
		ADC HL,BC
		LD A,E
		LD E,D
		LD D,L
		LD C,H
		LD B,0
		CALL RDFATS2
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
		RET

RDFATS1		LD BC,0
		LD A,E
		LD E,D
		LD D,C
RDFATS2		PUSH AF
		PUSH BC
		LD HL,FATSTR
		CALL BCDEHLP
		CALL LOADLST
		POP BC
		POP AF
		LD E,A
		LD D,0
		ADD HL,DE
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		RET

RDFATS0		LD H,D
		LD L,E
		ADD HL,HL
		ADD HL,DE
		SRL H
		RR L
		LD A,E
		LD E,H
		LD D,0
		LD B,D
		LD C,D
		SRL E
		PUSH AF
		PUSH HL
		LD HL,FATSTR
		CALL BCDEHLP
		CALL LOADLST
		POP BC
		LD A,B
		AND 1
		LD B,A
		ADD HL,BC
		LD B,(HL)
		INC HL
		LD A,H
		CP HIGH (BUF_512)+2
		JR NZ,RDFATS4
		PUSH BC
		LD BC,0
		INC DE
		CALL LOADLST
		POP BC
RDFATS4		POP AF
		LD D,(HL)
		LD E,B
		LD BC,0
		RRA
		JR NC,RDFATS3
		SRL D
		RR E
		SRL D
		RR E
		SRL D
		RR E
		SRL D
		RR E
RDFATS3		LD A,D
		AND 0X0F
		LD D,A
		RET

;бшвхякемхе пеюкэмнцн яейрнпю
;мю бунде BCDE=мнлеп FAT
;мю бшунде BCDE=юдпея яейрнпю
REALSEC		LD A,B
		OR C
		OR D
		OR E
		JR NZ,REALSE1
		LD DE,(FATSTR)
		LD BC,(FATSTR+2)
		LD HL,SEC_FAT
		PUSH HL
		CALL BCDEHLP
		POP HL
		JP BCDEHLP

REALSE1		LD HL,0XFFFE
		EX DE,HL
		ADD HL,DE
		EX DE,HL
		INC HL
		ADC HL,BC		;мнлеп йкюярепю-2
		LD A,(BYTSSEC)
		JR REALSE2

REALSE3		SLA E
		RL D
		RL L
		RL H
REALSE2		RRCA
		JR NC,REALSE3		;слмнфхкх мю пюглеп йкюярепю
		LD B,H
		LD C,L
		LD HL,STARTRZ
		CALL BCDEHLP		;опхаюбхкх ялеыемхе нр мювюкю дхяйю
		LD HL,FRSTDAT
		JP BCDEHLP		;опхаюбхкх ялеыемхе нр мювюкю пюгдекю

BCDE200		LD E,D
		LD D,C
		LD C,B
		LD B,0
		LD A,2
		JR BCDE_A

;BCDE>>A=BCDE
BCDE_A1		SRL B
		RR C
		RR D
		RR E
BCDE_A		RRCA
		JR NC,BCDE_A1
		RET

;(ADR)-BCDE=BCDE
BCDEHLM		LD A,(HL)
		INC HL
		SUB E
		LD E,A
		LD A,(HL)
		INC HL
		SBC A,D
		LD D,A
		LD A,(HL)
		INC HL
		SBC A,C
		LD C,A
		LD A,(HL)
		SBC A,B
		LD B,A
		RET

;(ADR)+BCDE=BCDE
BCDEHLP		LD A,(HL)
		INC HL
		ADD A,E
		LD E,A
		LD A,(HL)
		INC HL
		ADC A,D
		LD D,A
		LD A,(HL)
		INC HL
		ADC A,C
		LD C,A
		LD A,(HL)
		ADC A,B
		LD B,A
		RET

;HLDE+BC=HLDE
HLDEPBC		EX DE,HL
		ADD HL,BC
		EX DE,HL
		LD BC,0
		ADC HL,BC
		RET

F_EXT		DB "$C ",0

CP_EXT		PUSH BC
		PUSH DE
		PUSH HL
		LD C,0
		LD DE,F_EXT
CPETR2		LD A,(DE)
		AND A
		JR Z,CPETR1+1
		INC C
		PUSH DE
		PUSH HL
		CALL COMPARF
		POP HL
		POP DE
		JR Z,CPETR1
		INC DE
		INC DE
		INC DE
		JR CPETR2

CPETR1		LD A,C
		POP HL
		POP DE
		POP BC
		RET

COMPARF		PUSH DE
		LD DE,8
		ADD HL,DE
		POP DE
		LD B,3
COMPARF1	LD A,(DE)
		CP (HL)
		RET NZ
		INC HL
		INC DE
		DJNZ COMPARF1
		RET

;онхяй тюикнб он пюяьхпемхч х яйкюдхпнбюмхе хлем
;мю бунде: DE-юдпея йсдю яйкюдхпнбюрэ
FHOBETA		LD HL,0
		LD (KOLFIND),HL		;намскемхе йнкхвеярбю мюидеммшу тюикнб
FHOB2		PUSH DE
		CALL GETFZAP		;онксвемхе нохяюрекъ
		LD DE,0X0B		;мсфем ткюцнбши аюир нохяюрекъ
		EX DE,HL		;юдпея нохяюрекъ онпрхрэ ме асдел
		ADD HL,DE		;онксвхкх юдпея ткюцнбнцн аюирю
		LD L,(HL)		;бгъкх ткюцнбши аюир
		EX DE,HL		;бепмскх юдпея онксвеммнцн нохяюрекъ
		BIT 4,E			;опнбепйю щрн тюик хкх дхпю
		JR Z,FHOB3		;еякх тюик, рн опнднкфемхе опнбепйх
		POP HL			;дкъ дхпш опнбепърэ анкэье ме мюдн
		LD (HL),C
		INC HL
		LD (HL),B		;онкнфхкх мнлеп дхпш б рюакхвйс
		INC HL
		JR FHOB4		;онеуюкх й якедсчыелс нохяюрекч

FHOB3		CALL CP_EXT		;опнбепйю он пюяьхпемхч
		AND A
		POP DE
		JP Z,FHOB1
		PUSH DE			;еярэ янбоюдемхе я люяйни
		LD DE,0X14		;мюдн опнбепхрэ, ю пеюкэмн кх щрн унаерю?
		ADD HL,DE
		LD C,(HL)
		INC HL
		LD B,(HL)
		LD E,5
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)		;BCDE=мнлеп йкюярепю тюикю хг нохяюрекъ
;		LD (CLS_LOW),DE
;		LD (CLS_HIGH),BC
		CALL REALSEC		;оепебндхл б пеюкэмши мнлеп яейрнпю
		CALL LOADLST		;цпсгхл б оюлърэ оепбши яейрнп
		POP DE			;бепмскх юдпея яйкюдхпнбюмхъ
		LD A,(BUF_512+8)
		CP "C"			;опнбепйю врн щрн йнднбши акнй, ю ме врн-рн дпсцне
		JR NZ,FHOB1
		PUSH DE			;янупюмхкх юдпея яйкюдхпнбюмхъ дкъ дюкэмеиьеи опнбепйх
		LD B,H
		LD C,L			;бнгбпюыюеляъ б мювюкн яейрнпю
		CALL HOB_CRC		;явхрюел CRC HOBETA гюцнкнбйю
		LD A,(BC)
		INC BC
		LD E,A
		LD A,(BC)
		LD D,A			;б "DE" пюяявхрюммне CRC унаерю гюцнкнбйю
		AND A			;б "HL" бгърне хг ялеыемхъ +0X0F нр мювюкю тюикю
		SBC HL,DE		;япюбмхбюел 
		POP DE			;еякх ме янбоюкн рн бнглнфмн щрн ме унаерю
		JR NZ,FHOB1		;опносяйюел тюик
		EXX
		LD HL,(BUF_512+9)	;бгъкх юдпея гюцпсгйх
		DEC HL			;дкъ гюцпсгйх люяхлсл 40960 аюир (160 яейрнпнб)
		LD DE,(BUF_512+0X0B)	;бгъкх дкхмс йнднбнцн акнйю
		ADD HL,DE		;опнбепъел бшунд гю опедекш нгс
		EXX			;еякх бшкер гю опедек нгс
		JR C,FHOB1		;опносяйюел тюик
		EXX
		LD HL,(BUF_512+9)
		LD DE,0X6000		;опнбепъел юдпея гюцпсгйх
		AND A			;юдпея гюцпсгйх ме днкфем ашрэ мхфе 0X6000
		SBC HL,DE
		EXX			;еякх юдпея гюцпсгйх мхфе 0X6000
		JR C,FHOB1		;опносяйюел
		LD HL,(TEK_ZAP)
		EX DE,HL
		LD (HL),E
		INC HL
		LD (HL),D		;онкнфхкх мнлеп дхпш б рюакхвйс
		INC HL
;		LD HL,BUF_512
;		LD BC,8
;		LDIR			;оепемня б астеп тюикнбшбндхкйх
;		LD HL,TEK_ZAP
;		LDI
;		LDI
;		EX DE,HL
;		LD DE,0
;CLS_LOW		EQU $-2
;		LD (HL),E
;		INC HL
;		LD (HL),D
;		INC HL
;		LD DE,0
;CLS_HIGH	EQU $-2
;		LD (HL),E
;		INC HL
;		LD (HL),D
;		INC HL
;		INC HL
;		INC HL
FHOB4		EX DE,HL
		LD HL,(KOLFIND)
		INC HL			;+1 мюидеммшу тюикнб
		LD (KOLFIND),HL
;		LD BC,4
;		ADD IX,BC
		LD A,D
		CP HIGH (ADR_CAT)+0X10	;опнбепйю гю бшунд бшдекеммнцн астепю
		JR NC,END_HOB		;ме анкее 2048 дхпнб/тюикнб б рейсыеи дхпейрнпхх
FHOB1		PUSH DE
		LD HL,(TEK_ZAP)
		PUSH HL
		CALL NXTLEGZ		;хыел якедсчыхи нохяюрекэ
		LD BC,(TEK_ZAP)
		POP HL
		AND A
		SBC HL,BC		;опнбепйю мю хглемемхе мнлепю нохяюрекъ
		POP DE
		JP NZ,FHOB2		;мнлеп ме хглемхкяъ, бшундхл
END_HOB		LD BC,(KOLFIND)		;йнкхвеярбн мюидемшу тюикнб
		RET

;мю бунде: BC-юдпея гюцнкнбйю
;мю бшунде: HL-HOBETA CRC
HOB_CRC		LD A,0X0F
		LD HL,0
		EXX
		LD E,0
		EXX
HOBCRC1		EX AF,AF'
		LD A,(BC)
		LD E,A
		LD D,A
		ADD HL,DE
		EXX
		LD A,E
		INC E
		EXX
		LD E,A
		LD D,0
		ADD HL,DE
		EX AF,AF'
		INC BC
		DEC A
		JR NZ,HOBCRC1
		RET
